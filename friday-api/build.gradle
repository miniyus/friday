plugins {
	id "java"
	id "org.springframework.boot" version "3.1.3"
	id "io.spring.dependency-management" version "1.1.3"
	id "com.epages.restdocs-api-spec" version "0.18.4"
	id "org.hidetake.swagger.generator" version "2.18.2"
}

group = "com.miniyus"
version = "0.0.1-SNAPSHOT"

java {
	sourceCompatibility = "17"
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation "org.springframework.boot:spring-boot-starter-actuator"
	implementation "org.springframework.boot:spring-boot-starter-data-jpa"
	implementation "org.springframework.boot:spring-boot-starter-data-redis"
	implementation "org.springframework.boot:spring-boot-starter-data-rest"
	implementation "org.springframework.boot:spring-boot-starter-hateoas"
	implementation "org.springframework.boot:spring-boot-starter-oauth2-client"
	implementation "org.springframework.boot:spring-boot-starter-security"
	implementation "org.springframework.boot:spring-boot-starter-validation"
	implementation "org.springframework.boot:spring-boot-starter-web"
	implementation "org.springframework.boot:spring-boot-starter-aop"
	implementation "org.flywaydb:flyway-core"
	implementation "org.springframework.data:spring-data-rest-hal-explorer"
	implementation "com.auth0:java-jwt:4.4.0"
	implementation "com.google.code.findbugs:jsr305:3.0.2"
	implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.2.0"
	implementation "com.querydsl:querydsl-jpa:5.0.0:jakarta"
	implementation "com.querydsl:querydsl-apt"

	annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"
	annotationProcessor "jakarta.persistence:jakarta.persistence-api"
	annotationProcessor "jakarta.annotation:jakarta.annotation-api"

	testImplementation "org.projectlombok:lombok:1.18.28"

	compileOnly "org.projectlombok:lombok"
	developmentOnly "org.springframework.boot:spring-boot-devtools"
	runtimeOnly "com.h2database:h2"
	runtimeOnly "org.postgresql:postgresql"
	annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
	annotationProcessor "org.projectlombok:lombok"

	implementation "com.github.javafaker:javafaker:1.0.2"
	testImplementation "org.springframework.boot:spring-boot-starter-test"
	testImplementation "org.springframework.restdocs:spring-restdocs-mockmvc"
	testImplementation "com.epages:restdocs-api-spec-mockmvc:0.18.4"
	testImplementation "org.springframework.security:spring-security-test"
	testImplementation "org.testcontainers:testcontainers:1.19.0"
	testImplementation "org.testcontainers:junit-jupiter"  // TC 의존성
	testImplementation "org.testcontainers:postgresql"     // PostgreSQL 컨테이너 사용
	testImplementation "org.testcontainers:jdbc"
	testImplementation "com.tngtech.archunit:archunit:1.1.0"
}

tasks.named("test") {
	useJUnitPlatform()
}

tasks.withType(GenerateSwaggerUI) {
	dependsOn "openapi3"

	delete file("src/main/resources/static/docs")
	copy {
		from "build/resources/main/static/docs"
		into "src/main/resources/static/docs"
	}
}

bootJar {
	archivesBaseName = "friday"
	archiveFileName = "friday.jar"

	dependsOn "openapi3"
}

openapi3 {
	server = "http://127.0.0.1:8080"
	title = "Friday API"
	description = "Friday REST API Docs with Swagger UI."
	version = "0.0.1"
	outputFileNamePrefix = "friday-api"
	format = "yaml"
	outputDirectory = "build/resources/main/static/docs"
}

postman {
    title = "Friday API"
    version = "0.0.1"
    baseUrl = "http://127.0.0.1:8080"
	outputFileNamePrefix = "friday-api"
	outputDirectory = "build/resources/main/static/docs"
}

def querydslSrcDir = "src/main/generated"
sourceSets {
  main {
    java {
      srcDirs += [ querydslSrcDir ]
    }
  }
}

compileJava {
    options.compilerArgs << "-Aquerydsl.generatedAnnotationClass=javax.annotation.Generated"
}

tasks.withType(JavaCompile) {
	options.generatedSourceOutputDirectory = file(querydslSrcDir)
}

clean {
  delete file(querydslSrcDir)
}
